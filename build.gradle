plugins {
    id "com.diffplug.gradle.spotless" version "3.30.0"
}
subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'scala'
    apply plugin: 'com.diffplug.gradle.spotless'

    group = 'io.github.dreamylost'
    version = '2.0.0'

    description = "Recording Learning, Work, Interview, Summary and Reflection"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8


    repositories {
        //强制HTTPS了
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven { url "https://repo.maven.apache.org/maven2" }
    }

    sourceSets {
        main {
            scala {
                srcDirs = ['src/main/java', 'src/main/scala']
            }
            java {
                srcDirs = []
            }
        }

        //测试的执行可能有问题，可以用IDEA rebuild单独的子模块，再执行脚本build
        test {
            scala {
                srcDirs = ['src/test/scala', 'src/test/java']
            }
            java {
//                srcDirs = ['src/test/java']
                //排除旧的单元测试类
//                exclude 'io/github/dreamylost/practice/test/**'
            }
        }
    }

    /**
     * 所有项目共用Scala2.12.8
     *
     * sbt有点慢，所以用了gradle，其他sbt项目不好继承进来，PS：降低编译速度
     */
    dependencies {
        //because some test write in src/main/java
        compile group: 'junit', name: 'junit', version: '4.12'
        compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
        compile group: 'org.scala-lang', name: 'scala-library', version: '2.12.8'
        compile group: 'org.scala-lang', name: 'scala-reflect', version: '2.12.8'
        compile group: 'com.diffplug.spotless', name: 'spotless-plugin-gradle', version: '4.5.0'
        compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    }

    [compileJava, compileScala, compileTestScala, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    /**
     * gradle spotlessApply
     * 自动格式化Java
     */
    spotless {
        java {
            removeUnusedImports()
            googleJavaFormat('1.6').aosp()
            licenseHeader '/* All Contributors (C) $YEAR */'
        }
    }
}

/**
 * gradle  spotlessApply
 * 格式化所有Scala项目
 */
project(':scala-leetcode') {
    spotless {
        scala {
            scalafmt('2.6.1').configFile('.scalafmt.conf')
            licenseHeader '/* Licensed under Apache-2.0 (C) All Contributors */', 'package '
        }
    }
}

project(':scala-utils') {
    spotless {
        scala {
            scalafmt('2.6.1').configFile('.scalafmt.conf')
            licenseHeader '/* Licensed under Apache-2.0 @梦境迷离 */', 'package '
        }
    }
}

project(':scala-examples') {
    spotless {
        scala {
            scalafmt('2.6.1').configFile('.scalafmt.conf')
            licenseHeader '/* Licensed under Apache-2.0 @梦境迷离 */', 'package '
        }
    }
}

project(':kotlin-leetcode') {
    spotless {
        kotlin {
            ktlint().userData(['indent_size': '2', 'continuation_indent_size' : '2'])
            licenseHeader '/* Licensed under Apache-2.0 @梦境迷离 */'
        }
    }
}

project(':kotlin-study') {
    spotless {
        kotlin {
            ktlint().userData(['indent_size': '2', 'continuation_indent_size' : '2'])
            licenseHeader '/* Licensed under Apache-2.0 @梦境迷离 */'
        }
    }
}