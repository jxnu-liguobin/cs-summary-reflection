version = '0.0.1'
group = 'io.github.dreamylost'

buildscript {
    ext.kotlin_version = '1.3.71'

    //编译插件的依赖仓库
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        maven {
            name "aliyunmaven"
            url "https://maven.aliyun.com/nexus/content/groups/public/"
        }
    }

    //gradle的kotlin插件
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'

configurations {
    ktlint
}

sourceSets {
    main {
        kotlin {
            srcDirs = ['src/main/kotlin']
        }
    }
    test {
        kotlin {
            srcDirs = ['src/test/kotlin']
        }
    }
}

//项目本身的依赖仓库
repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven { url "https://repo.maven.apache.org/maven2" }
    maven {
        name "aliyunmaven"
        url "https://maven.aliyun.com/nexus/content/groups/public/"
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version
    ktlint "com.pinterest:ktlint:0.37.2"

}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/main/kotlin/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/main/kotlin/**/*.kt"
}